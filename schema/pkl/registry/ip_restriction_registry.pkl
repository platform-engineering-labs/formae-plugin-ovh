/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

/// OVH Container Registry IP Restriction
/// Manages access restrictions for the registry
/// API: PUT /cloud/project/{serviceName}/containerRegistry/{registryId}/ipRestrictions/management
module ovh.registry.ip_restriction

import "@formae/formae.pkl"
import "../ovh.pkl"

const type = "OVH::Registry::IpRestriction"

/// IP restriction target type
typealias IpRestrictionType = "management"|"registry"

/// Resolvable reference to an IP Restriction
open class IpRestrictionResolvable extends formae.Resolvable {
  hidden type = module.type

  hidden ipBlock: IpRestrictionResolvable = (this) { property = "ipBlock" }
}

@ovh.ResourceHint {
  type = module.type
  identifier = "ipBlock"
}
open class IpRestriction extends formae.Resource {
  hidden parent = this

  /// Cloud project service name (project ID)
  @ovh.FieldHint { required = true; createOnly = true }
  serviceName: String

  /// Registry ID
  @ovh.FieldHint { required = true; createOnly = true }
  registryId: (String|formae.Resolvable)

  /// IP block in CIDR notation (e.g., "192.168.1.0/24")
  @ovh.FieldHint { required = true; createOnly = true }
  ipBlock: String

  /// Restriction type: "management" for Harbor UI, "registry" for Docker API
  @ovh.FieldHint { required = true; createOnly = true }
  type: IpRestrictionType

  /// Description of this IP restriction
  description: String?

  hidden res: IpRestrictionResolvable = new {
    label = parent.label
    stack = parent.stack?.label
  }
}
