/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

module instance

import "@formae/formae.pkl"
import "../ovh.pkl"

const type = "OVH::Compute::Instance"

/// Resolvable reference to an Instance resource
/// Use this to reference an instance's properties in dependent resources
open class InstanceResolvable extends formae.Resolvable {
  hidden type = module.type

  /// The instance's unique identifier
  hidden id: InstanceResolvable = (this) {
    property = "id"
  }

  /// The instance's name
  hidden name: InstanceResolvable = (this) {
    property = "name"
  }
}

@ovh.ResourceHint {
  type = module.type
  identifier = "id"
}
open class Instance extends formae.Resource {
  @ovh.FieldHint {
    required = true
  }
  name: String

  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  flavorId: String

  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  imageId: String

  /// Instance region (e.g., "DE1", "GRA7", "BHS5").
  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  region: String

  @ovh.FieldHint {
    createOnly = true
  }
  networks: Listing<NetworkAttachment>?

  @ovh.FieldHint {
    createOnly = true
  }
  sshKeyId: String?

  @ovh.FieldHint {
    createOnly = true
  }
  userData: String?

  @ovh.FieldHint {
    createOnly = true
  }
  availabilityZone: String?

  // Computed fields (not user-provided)
  // id: String
  // status: String

  local parent = this

  /// Provides resolvable references to this instance's properties
  hidden res: InstanceResolvable = new {
    label = parent.label
    stack = parent.stack?.label
  }
}

@ovh.SubResourceHint
open class NetworkAttachment extends formae.SubResource {
  /// Private or public network ID.
  networkId: (String|formae.Resolvable)?

  /// Static IP (can only be defined for private networks).
  ip: String?
}
