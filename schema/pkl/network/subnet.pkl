/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

module subnet

import "@formae/formae.pkl"
import "../ovh.pkl"

const type = "OVH::Network::Subnet"

/// Resolvable reference to a Subnet resource
/// Use this to reference a subnet's properties in dependent resources
open class SubnetResolvable extends formae.Resolvable {
  hidden type = module.type

  /// The subnet's unique identifier
  hidden id: SubnetResolvable = (this) {
    property = "id"
  }

  /// The subnet's network ID
  hidden network_id: SubnetResolvable = (this) {
    property = "network_id"
  }
}

@ovh.ResourceHint {
  type = module.type
  identifier = "id"
}
open class Subnet extends formae.Resource {
  // NOTE: Region is read from target config

  /// Parent network ID. The subnet will be created within this network.
  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  network_id: String|formae.Resolvable

  /// Subnet range in CIDR notation (e.g., "10.0.0.0/24").
  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  cidr: String

  /// IP version (4 or 6).
  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  ipVersion: Int

  /// Enable DHCP for this subnet.
  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  enableDhcp: Boolean

  /// Enable gateway IP for this subnet.
  /// If true, a gateway will be assigned to the subnet.
  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  enableGatewayIp: Boolean

  /// Subnet name.
  @ovh.FieldHint {
    required = true
    createOnly = true
  }
  name: String

  /// Specific gateway IP address.
  /// Only used when enableGatewayIp is true.
  @ovh.FieldHint {
    required = false
    createOnly = true
  }
  gatewayIp: String?

  /// DNS nameservers for the subnet.
  @ovh.FieldHint {
    required = false
    createOnly = true
  }
  dnsNameServers: Listing<String>?

  /// Use default public DNS resolver.
  @ovh.FieldHint {
    required = false
    createOnly = true
  }
  useDefaultPublicDNSResolver: Boolean?

  /// IP allocation pools for DHCP.
  @ovh.FieldHint {
    required = false
    createOnly = true
  }
  allocationPools: Listing<AllocationPool>?

  /// Host routes for the subnet.
  @ovh.FieldHint {
    required = false
    createOnly = true
  }
  hostRoutes: Listing<HostRoute>?

  local parent = this

  /// Provides resolvable references to this subnet's properties
  hidden res: SubnetResolvable = new {
    label = parent.label
    stack = parent.stack?.label
  }
}

/// IP allocation pool for DHCP
@ovh.SubResourceHint
open class AllocationPool extends formae.SubResource {
  /// Start IP address of the allocation range
  start: String
  /// End IP address of the allocation range
  end: String
}

/// Host route for subnet routing
@ovh.SubResourceHint
open class HostRoute extends formae.SubResource {
  /// Destination CIDR for the route (eg: 192.168.1.0/24)
  destination: String
  /// Next hop IP address (eg: 192.168.1.254)
  nextHop: String
}
