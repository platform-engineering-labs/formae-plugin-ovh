/*
 * Â© 2025 Platform Engineering Labs Inc.
 *
 * SPDX-License-Identifier: FSL-1.1-ALv2
 */

amends "@formae/forma.pkl"

import "@ovh/network/network.pkl"
import "@ovh/compute/instance.pkl"
import "@ovh/network/floatingip.pkl"

import "./vars.pkl" as v

forma {
  v.stack
  v.target

  // Create a private network with subnet and gateway for floating IP support
  local testNetwork = new network.Network {
    label = "plugin-sdk-test-network-floatingip"
    name = "formae-plugin-sdk-test-network-floatingip-\(v.testRunID)"
    subnet = new network.SubnetConfig {
      cidr = "10.0.10.0/24"
      ipVersion = 4
      enableDhcp = true
      enableGatewayIp = true
      name = "formae-plugin-sdk-test-subnet-floatingip-\(v.testRunID)"
    }
    // Gateway (router) is required for floating IP attachment
    gateway = new network.GatewayConfig {
      model = "s"
      name = "formae-plugin-sdk-test-gateway-floatingip-\(v.testRunID)"
    }
  }
  testNetwork

  // Instance IP on the private network (fixed for floating IP association)
  local instancePrivateIP = "10.0.10.10"

  // Create an instance on the private network with a fixed IP
  local testInstance = new instance.Instance {
    label = "plugin-sdk-test-instance-floatingip"
    name = "formae-plugin-sdk-test-instance-floatingip-\(v.testRunID)"
    flavorId = v.flavorID
    imageId = v.ubuntuImageID
    region = v.region
    networks = new Listing {
      // Private network with fixed IP
      new instance.NetworkAttachment {
        networkId = testNetwork.res.id
        ip = instancePrivateIP
      }
    }
  }
  testInstance

  // Create a floating IP and attach it to the instance
  // Path: POST /cloud/project/{serviceName}/region/{regionName}/instance/{instanceId}/floatingIp
  new floatingip.FloatingIP {
    label = "plugin-sdk-test-floatingip"
    instance_id = testInstance.res.id
    ip = instancePrivateIP
  }
}
